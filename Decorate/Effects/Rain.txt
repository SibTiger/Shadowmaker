//Documentation
//=================
//Place the RainDropSpawner on your map, and use values set on the specials tab to modify it

//The first argument controls it's area. 128, for example makes it a 256x256 square or 256-radius circle (See fourth argument)
//The second argument controls it's frequency. The lower the number, the heavier the rainfall
//The third argument controls whether or not it plays it's ambient sound. 0 will play the sound, 1 will not
//The fourth argument controls the area type. 0 is a square, 1 is a circle



Actor RainSpawner 15000
{
    // GZDoom Builder Configuration
    // ==============================
        //$Title "Rain"
        //$Category "Special Effects"
        //$Color 9
        //$Sprite RNDRA0
        //$NotAngled
        // ----
        //$Arg0 "Area"
        //$Arg0Type 0
        //$Arg0ToolTip "Rain spawner size that covers the area.\nUsage example:\n128 = 256x256 (or 256-radius circle)"
        //$Arg0Default 32
        // ----
        //$Arg1 "Frequency"
        //$Arg1Type 0
        //$Arg1ToolTip "Rain frequency of the spawner; lower the value the harder it rains."
        //$Arg1Default 0
        // ----
        //$Arg2 "Sound"
        //$Arg2Type 11
        //$Arg2ToolTip "When set to '0', this will give the rain spawner sound."
        //$Arg2Default 1
        //$Arg2Enum {0 = "Enable sound"; 1 = "Disable sound";}
        // ----
        //$Arg3 "Type"
        //$Arg3Type 11
        //$Arg3ToolTip "Changes the spawner type to cubic or a circle."
        //$Arg3Default 0
        //$Arg3Enum {0 = "Square"; 1 = "Circle";}
    // ------------------------------
    // ==============================


    Radius 1
    Height 1
    +NoClip
    +ClientSideOnly
    +SpawnCeiling
    +NoGravity
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(Args[2] > 0, "NoSound")
        TNT1 A 0 A_JumpIf(Args[3] > 0, "Circle")
        TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7", 1)
        TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
        Loop
    Circle:
        TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 360), 128, Args[1])
    NoSound:
        TNT1 A 0 A_Jumpif(Args[3] > 0, "NoSoundCircle")
        TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
        Loop
    NoSoundCircle:
        TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 360), 128, Args[1])
        Loop
    }
}




Actor RainDrop
{

    // Standards
    // ===========
        +ForceYBillboard // Usable only for when Voxels is disabled and using Sprites.
    // ------
    // ======

    +Missile
    +NoBlockMap
    -NoGravity
    // +DontSplash // Because rain is used where splashes are present, with the flag 'false' - this will cause all kinds of havoc with performance. [NG]
    Height 2
    Radius 2
    Mass 9 // Small TERRIAN splashes [NG]
    Renderstyle Translucent
    Alpha 0.6
    Scale 0.2
    States
    {
        Spawn:
        RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
        Loop
    Death:
        RNDR BCDEFGH 3 A_FadeOut(0.15)
        Stop
    }
}